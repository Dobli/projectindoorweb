plugins {
    id 'java'
    id 'idea'
    id 'com.craigburke.client-dependencies' version '1.4.0'
    id 'org.springframework.boot' version '1.5.7.RELEASE'
    id 'checkstyle'
    id 'pmd'
    id 'findbugs'
    id 'com.palantir.docker' version '0.13.0'
    id "com.eriwen.gradle.js" version "2.14.1"
}

group 'de.hft-stuttgart'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def versions = [
        checkstyle: '8.3',
]

repositories {
    mavenCentral()
    jcenter()
}

jar {
    baseName = 'projectindoorweb'
    version = '0.1.0'
}

// JavaScript handling
combineJs {
    encoding = "UTF-8"
    source = [
            "${projectDir}/src/main/resources/static/assets/vendor/angular/angular.js",
            "${projectDir}/src/main/resources/static/assets/vendor/angular/modules/angular-animate.js",
            "${projectDir}/src/main/resources/static/assets/vendor/angular/modules/angular-aria.js",
            "${projectDir}/src/main/resources/static/assets/vendor/angular/modules/angular-messages.js",
            "${projectDir}/src/main/resources/static/assets/vendor/angular/modules/angular-route.js",
            "${projectDir}/src/main/resources/static/assets/vendor/angular/modules/angular-sanitize.js",
            "${projectDir}/src/main/resources/static/assets/vendor/angular-material/angular-material.js",
            "${projectDir}/src/main/resources/static/assets/vendor/openlayers/ol.js",
            "${projectDir}/src/main/resources/static/assets/vendor/angular-openlayers-directive/angular-openlayers-directive.js",
            "${projectDir}/src/main/resources/static/assets/vendor/jquery/jquery.js",
            "${projectDir}/src/main/resources/static/js/app.js"
    ]
    dest = file("${projectDir}/src/main/resources/static/assets/all-indoor.js")
}

configurations {
    checkstyleConfig
}

checkstyle {
    ignoreFailures = true
    toolVersion = "${versions.checkstyle}"
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'sun_checks.xml')
}

pmd {
    ignoreFailures = true
    sourceSets = [sourceSets.main]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSets = [
            "java-basic",
            "java-braces"
    ]
}

findbugs {
    ignoreFailures = true
    sourceSets = [sourceSets.main]
}

docker {
    name "hftstuttgart/${jar.baseName}"
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
}

clientDependencies {
    installDir = 'src/main/resources/static/assets/vendor'
    npm {
        'jquery'('3.2.1')
        'angular'('1.5.5')
        'angular-animate'('1.5.5', into: 'angular/modules')
        'angular-aria'('1.5.5', into: 'angular/modules')
        'angular-messages'('1.5.5', into: 'angular/modules')
        'angular-route'('1.5.5', into: 'angular/modules')
        'angular-sanitize'('1.5.5', into: 'angular/modules')
        'angular-material'('1.1.0')
        'openlayers'('3.18.1', transitive: false)
        'angular-openlayers-directive'('1.20.1', transitive: false)
        'material-design-icons'('3.0.1', from: 'iconfont', transitive: false)
    }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-devtools")
    compile("org.springframework.boot:spring-boot-starter-test")
    compile("org.apache.commons:commons-csv:1.5")
    compile("commons-fileupload:commons-fileupload:1.2.1")
    compile 'mysql:mysql-connector-java'
    compile("com.h2database:h2")
    compile "io.springfox:springfox-swagger2:2.7.0"
    compile 'io.springfox:springfox-swagger-ui:2.7.0'
    checkstyleConfig("com.puppycrawl.tools:checkstyle:${versions.checkstyle}") { transitive = false }
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

